SELECT DISTINCT ?item ?itemLabel ?term ?termLabel ?constituency ?constituencyLabel ?party ?partyLabel ?start ?election ?electionLabel ?end ?cause ?causeLabel {
 ?item p:P39 ?positionStatement .
       { ?positionStatement ps:P39 wd:Q41582627 . } # 51st - 1992-1997
 union { ?positionStatement ps:P39 wd:Q41582624 . } # 50th - 1987-1992
 union { ?positionStatement ps:P39 wd:Q41582621 . } # 49th - 1983-1987
 union { ?positionStatement ps:P39 wd:Q41582619 . } # 48th - 1979-1983
 union { ?positionStatement ps:P39 wd:Q41582617 . } # 47th - 1974-1979
 union { ?positionStatement ps:P39 wd:Q41582615 . } # 46th - 1974-1974
 union { ?positionStatement ps:P39 wd:Q41582612 . } # 45th - 1970-1974
 OPTIONAL { ?positionStatement pq:P768 ?constituency . }
 OPTIONAL { ?positionStatement pq:P4100 ?party . }
 OPTIONAL { ?positionStatement pq:P580 ?start . }
 OPTIONAL { ?positionStatement pq:P2715 ?election . }
 OPTIONAL { ?positionStatement pq:P582 ?end . }
 OPTIONAL { ?positionStatement pq:P1534 ?cause . }
 OPTIONAL { ?positionStatement pq:P2937 ?term . }
 SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }
}
ORDER BY ?item ?start
