SELECT DISTINCT ?item ?itemLabel ?term ?termLabel ?constituency ?constituencyLabel ?party ?partyLabel ?start ?election ?electionLabel ?end ?cause ?causeLabel {
 ?item p:P39 ?positionStatement .
       { ?positionStatement ps:P39 wd:Q35494253 . } # 55th - 2010-15 onwards
 union { ?positionStatement ps:P39 wd:Q35647955 . } # 54th - 2005-10
 union { ?positionStatement ps:P39 wd:Q35921591 . } # 53rd - 2001-05
 union { ?positionStatement ps:P39 wd:Q36634044 . } # 52nd - 1997-2001
 OPTIONAL { ?positionStatement pq:P768 ?constituency . }
 OPTIONAL { ?positionStatement pq:P4100 ?party . }
 OPTIONAL { ?positionStatement pq:P580 ?start . }
 OPTIONAL { ?positionStatement pq:P2715 ?election . }
 OPTIONAL { ?positionStatement pq:P582 ?end . }
 OPTIONAL { ?positionStatement pq:P1534 ?cause . }
 OPTIONAL { ?positionStatement pq:P2937 ?term . }
 SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }
}
ORDER BY ?item
