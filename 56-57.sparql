SELECT DISTINCT ?item ?itemLabel ?term ?termLabel ?constituency ?constituencyLabel ?party ?partyLabel ?start ?election ?electionLabel ?end ?cause ?causeLabel {
 ?item p:P39 ?positionStatement .
       { ?positionStatement ps:P39 wd:Q30524710 . } # 57th - 2017 onwards
 union { ?positionStatement ps:P39 wd:Q30524718 . } # 56th - 2015-17
 OPTIONAL { ?positionStatement pq:P768 ?constituency . }
 OPTIONAL { ?positionStatement pq:P4100 ?party . }
 OPTIONAL { ?positionStatement pq:P580 ?start . }
 OPTIONAL { ?positionStatement pq:P2715 ?election . }
 OPTIONAL { ?positionStatement pq:P582 ?end . }
 OPTIONAL { ?positionStatement pq:P1534 ?cause . }
 OPTIONAL { ?positionStatement pq:P2937 ?term . }
 SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }
}
ORDER BY ?item ?start
